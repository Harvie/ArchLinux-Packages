# Contributor: Thomas Mudrunka <harvie@@email..cz>
# Maintainer: Thomas Mudrunka <harvie@@email..cz>
# You can also contact me on http://blog.harvie.cz/

# -Original Maintainer: Eric Belanger <eric@archlinux.org>
# -Original Contributor: Sean Middleditch <elanthis@awesomeplay.com>

pkgname=xscreensaver-arch-logo
_original_pkgname=xscreensaver
pkgver=5.11
pkgrel=1
pkgdesc="Screen saver and locker for the X Window System. Version with replaced art/logos/graphics to better fit to the ArchLinux (Gnome,KDE,XFCE,...). Also adds enchanced desktop icon."
arch=('i686' 'x86_64')
url="http://www.jwz.org/xscreensaver/"
license=('BSD')
depends=('libxxf86misc' 'libglade' 'mesa' 'pam' 'xorg-res-utils')
makedepends=('bc')
conflicts=('xscreensaver')
provides=('xscreensaver')
backup=('etc/pam.d/xscreensaver')
source=(http://www.jwz.org/xscreensaver/${_original_pkgname}-${pkgver}.tar.gz \
	add-electricsheep.diff xscreensaver.pam LICENSE
	logo-50.xpm logo-180.xpm logo-50.png logo-180.png)
md5sums=('80f981b86734ccb31c5b3b24af3930ae'
         'f82524626ed8a6832cd279e0548a9b46'
         '367a3538f54db71f108b34cfa31088ac'
         '5e7f3f2a63d20a484742f5b4cb5d572c'
         '3ab3668446f774f3539cf80a6538c188'
         '6c02caa437038fe275bbed7c94f1f982'
         '5c07dfa0c24edf141db09546bd11cc12'
         '04f820ef64a2b9580ad40fa389885133')

build() {
	cd "${srcdir}/${_original_pkgname}-${pkgver}"
	patch -Np0 -i "${srcdir}/add-electricsheep.diff" || return 1

	cd ${srcdir}
	cp -f logo-180.xpm ${_original_pkgname}-${pkgver}/utils/images/

  cd "${srcdir}/${_original_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --with-x-app-defaults=/usr/share/X11/app-defaults \
    --with-pam --without-motif --with-gtk --without-gnome --with-xml --with-gl \
    --without-gle --with-xpm --with-pixbuf --with-jpeg || return 1
  (cd hacks ; make m6502.h)
  (cd hacks/glx ; make molecules.h)
  make || return 1
}

package() {
  cd "${srcdir}/${_original_pkgname}-${pkgver}"
  make install_prefix="${pkgdir}" install || return 1
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${_original_pkgname}/LICENSE" || return 1
  install -D -m644 ../xscreensaver.pam "${pkgdir}/etc/pam.d/xscreensaver" || return 1
  chmod 755 "${pkgdir}/usr/bin/xscreensaver" || return 1
  #echo "NotShowIn=KDE;GNOME;" >> "${pkgdir}/usr/share/applications/xscreensaver-properties.desktop" || return 1
}
