# Contributor:	Thomas Mudrunka <harvie@@email..cz>
# Maintainer:		Thomas Mudrunka <harvie@@email..cz>
# You can also contact me on http://blog.harvie.cz/

pkgname=apparmor
pkgver=2.5.1
pkgrel=3
pkgdesc="Linux application security framework - mandatory access control for programs"
arch=(i686 x86_64)
license=(GPL)
url='https://launchpad.net/apparmor'
depends=(perl perl-locale-gettext perl-term-readkey perl-rpc-xml)
makedepends=(autoconf automake make gcc swig perl python2 ruby wxgtk audit)
optdepends=('perl: many apparmor utilities' 'perl-rpc-xml: more utilities')
provides=(apparmor-parser libapparmor apparmor-utils apparmor-profiles apparmor-notify
	apparmor-lib apparmor-perl apparmor-python apparmor-ruby
	apparmor-dbus apparmor-profile-editor apparmor-applet
)
bigver="$(echo $pkgver | cut -d . -f -2)"
source=("http://launchpad.net/apparmor/$bigver/$pkgver/+download/apparmor-$pkgver.tar.gz")
md5sums=('76b37656bf42fedab0d0b9d47e690a8b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	#Patch
	patch=common/Make.rules; { rm "$patch"
		sed -e 's/\/usr\/bin\/pod2man/\/usr\/lib\/perl5\/core_perl\/bin\/pod2man/g' |
		sed -e 's/\/usr\/bin\/pod2html/\/usr\/lib\/perl5\/core_perl\/bin\/pod2html/g' > "$patch"
	} < "$patch"

	patch=parser/tst/Makefile; { rm "$patch"
		sed -e 's/\/usr\/bin\/prove/\/usr\/lib\/perl5\/core_perl\/bin\/prove/g' > "$patch"
	} < "$patch"
	patch=parser/Makefile; { rm "$patch"
		sed -e 's/pdflatex/true/g' > "$patch" #just workaround until we'll get pdflatex package
	} < "$patch"
	echo '#!/bin/true' > parser/tst/caching.sh #Can't pass this test with current kernel


	#Build (these sections can be used for splitpkg in future...)
	( cd parser
		make
		make install DESTDIR=${pkgdir}
		)
	( cd libraries/libapparmor
		export PYTHON=/usr/bin/python2
		./autogen.sh
		./configure --prefix=/usr --with-perl --with-python --with-ruby
		make
		make install DESTDIR=${pkgdir}
		)
	( cd utils
		make
		make install DESTDIR=${pkgdir}
		)
	( cd profiles
		make
		make install DESTDIR=${pkgdir}
		)
	( cd deprecated/management/profile-editor
		./macros/autogen.sh
		./configure --prefix=/usr
		make
		make install DESTDIR=${pkgdir}
		)
	( cd deprecated/management/apparmor-dbus
		./autogen.sh
		./configure --prefix=/usr
		make
		make install DESTDIR=${pkgdir}
		)
	#( cd deprecated/management/applets/apparmorapplet-gnome
	#	./autogen.sh
	#	./configure --prefix=/usr
	#	make
	#	make install DESTDIR=${pkgdir}
	#	)
}
