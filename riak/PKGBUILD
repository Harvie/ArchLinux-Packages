# Contributor:	Thomas Mudrunka <harvie@@email..cz>
# Maintainer:		Thomas Mudrunka <harvie@@email..cz>
# You can also contact me on http://blog.harvie.cz/

pkgname=riak
pkgver=0.11.0
pkgrel=1
pkgdesc="NOSQL database engine providing decentralized key-value store, flexible map/reduce engine and HTTP/JSON query interface"
arch=(i686 x86_64)
license=('Apache 2')
url='http://riak.basho.com/'
makedepends=(erlang make gcc pacman)
pkgbigver=$( echo $pkgver | grep -o ^[0-9]*.[0-9]* ) #get x.x version from x.x.x version
backup=('opt/riak/etc/app.config' 'opt/riak/etc/vm.args')
install="${pkgname}.install"
source=("http://downloads.basho.com/riak/${pkgname}-${pkgbigver}/${pkgname}-${pkgver}.tar.gz" 'riak.rc')
md5sums=('5792ac2b91ad71ccaee91d1ef9632348'
         '942e198d5dfa52ac36360e3f79b65930')
prefixpath="/opt/${pkgname}"
#prefixpath="/usr/lib/erlang"
#prefixpath="/usr/lib/erlang/lib/${pkgname}-${pkgver}"

build() {
	cd ${srcdir}/riak-${pkgver} || return 1
	msg Cleaning...;
	make distclean
	msg Building...;
	LDFLAGS='' make rel || return 1

	cd rel/riak || return 1
	#cd rel/overlay || return 1
	mkdir -p ${pkgdir}${prefixpath}

	msg Packing...;
	cp -r ./* ${pkgdir}${prefixpath} || return 1

	[ -d "${pkgdir}/usr/lib/erlang" ] && {
		msg 'Removing files already shipped with erlang package...';
		pacman -Ql erlang | cut -d ' ' -f 2- | while read i; do
			[ -d "$i" ] || {
				rm -f ${pkgdir}/"$i";
				echo -ne "$i                             \r";
			}
		done; echo;
	}

	chmod -R 755 ${pkgdir}${prefixpath}/bin

	msg 'ArchLinux integration...';

	mkdir -p ${pkgdir}/var/log
	ln -s ${prefixpath}/log ${pkgdir}/var/log/riak
	mkdir -p ${pkgdir}/etc/rc.d
	ln -s ${prefixpath}/etc ${pkgdir}/etc/riak
	mkdir -p ${pkgdir}/usr/share/doc/
	cp -r ${srcdir}/riak-${pkgver}/doc/man ${pkgdir}/usr/share/
	cp -r ${srcdir}/riak-${pkgver}/doc ${pkgdir}/usr/share/doc/riak
	rm -r ${pkgdir}/usr/share/doc/riak/man

	mkdir -p ${pkgdir}/usr/bin/
	for i in riak riak-admin; do
		cat > ${pkgdir}/usr/bin/$i <<EOF
#!/bin/sh
su riak -c "${prefixpath}/bin/$i \$@"
EOF
	done

	cp ${srcdir}/riak.rc ${pkgdir}/etc/rc.d/riak

	chmod -R 755 ${pkgdir}/usr/bin/ ${pkgdir}/etc/rc.d/
}
